/*******************************************************************************
 * Copyright (c) 2014-2016 Akos Horvath, Abel Hegedus, Zoltan Ujhelyi, Denes Harmath, IncQuery Labs Ltd.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-v20.html.
 * 
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
package org.eclipse.viatra.examples.cps.xform.m2m.incr.expl.queries

import "http://org.eclipse.viatra/model/cps"
import "http://org.eclipse.viatra/model/deployment"
import "http://org.eclipse.viatra/model/cps-traceability"
import java org.eclipse.viatra.examples.cps.xform.m2m.util.SignalUtil

pattern mappedCPS(cps2dep : CPSToDeployment, cps : CyberPhysicalSystem, deployment : Deployment) {
	CPSToDeployment.cps(cps2dep, cps);
	CPSToDeployment.deployment(cps2dep, deployment);
}

pattern cps2depTrace(cps2dep : CPSToDeployment, trace : CPS2DeploymentTrace, cpsElement : Identifiable, depElement : DeploymentElement) {
	CPSToDeployment.traces(cps2dep, trace);
	CPS2DeploymentTrace.cpsElements(trace, cpsElement);
	CPS2DeploymentTrace.deploymentElements(trace, depElement);
}

pattern identifiable(identifiable : Identifiable) {
	Identifiable(identifiable);
}

pattern deploymentElements(depElement : DeploymentElement) {
	DeploymentElement(depElement);
}

pattern illegalTrace(trace : CPS2DeploymentTrace) {
	find cps2depTrace(_, trace, cpsElem, depElem);
	neg find identifiable(cpsElem);
	neg find deploymentElements(depElem);
}

/* ============================================
 * ==== 		                 		   ====
 * ====			    HOST RULES 			   ====
 * ====		                 			   ====
 * ============================================ */

pattern hostInstances(hostType : HostType, hostInstance : HostInstance) {
	HostType.instances(hostType, hostInstance);
}

pattern mappedHostInstance(hostInstance : HostInstance, depHost : DeploymentHost) {
	find hostInstances(_, hostInstance);
	find cps2depTrace(_, _, hostInstance, depHost);
	Deployment.hosts(_, depHost);
}

pattern monitoredHostInstance(hostInstance : HostInstance) {
	find mappedHostInstance(hostInstance, _depHost);
}

pattern unmappedHostInstance(hostType : HostType, hostInstance : HostInstance) {
	find hostInstances(hostType, hostInstance);
	neg find cps2depTrace(_, _, hostInstance, _);
}

pattern deletedDeploymentHost(depHost : DeploymentHost, trace : CPS2DeploymentTrace) {
	neg find hostInstances(_, host);
	find cps2depTrace(_, trace, host, depHost);
}

/* ============================================
 * ==== 		                 		   ====
 * ====			APPLICATION RULES 		   ====
 * ====		                 			   ====
 * ============================================ */
 
pattern allocatedApplicationInstances(appType : ApplicationType, appInstance : ApplicationInstance, host : HostInstance) {
	find applicationInstance(appType, appInstance, host);
	find hostInstances(_, host);
}

pattern applicationInstance(appType : ApplicationType, appInstance : ApplicationInstance, host : HostInstance){
	ApplicationType.instances(appType, appInstance);
	ApplicationInstance.allocatedTo(appInstance, host);
}

pattern deploymentHostApplications(depHost : DeploymentHost, depApp : DeploymentApplication) {
	DeploymentHost.applications(depHost, depApp);
}

pattern mappedApplicationInstance(appInstance : ApplicationInstance, depApp : DeploymentApplication, host : HostInstance, depHost : DeploymentHost) {
	find allocatedApplicationInstances(_, appInstance, host);
	find mappedHostInstance(host, depHost);
	find cps2depTrace(_, _, appInstance, depApp);
	find deploymentHostApplications(_, depApp);
}

pattern monitoredApplicationInstance(appInstance : ApplicationInstance, host : HostInstance) {
	find mappedApplicationInstance(appInstance, _depApp, host, _depHost);
}

pattern unmappedApplicationInstance(appType : ApplicationType, appInstance : ApplicationInstance, depHost : DeploymentHost) {
	find allocatedApplicationInstances(appType, appInstance, host);
	find mappedHostInstance(host, depHost);
	neg find cps2depTrace(_, _, appInstance, _);
}

pattern deletedApplicationInstance(depApp : DeploymentElement, trace : CPS2DeploymentTrace) {
	neg find applicationInstance(_, app, _);
	DeploymentApplication(depApp);
	find cps2depTrace(_, trace, app, depApp);
} or {
	find applicationInstance(_, app, host);
	neg find hostInstances(_, host);
	find cps2depTrace(_, trace, app, depApp);
}

/* ============================================
 * ==== 		                 		   ====
 * ====		   STATE MACHINE RULES 		   ====
 * ====		                 			   ====
 * ============================================ */
 
pattern allocatedStateMachines(appInstance : ApplicationInstance, stateMachine : StateMachine) {
	find applicationBehavior(appType, stateMachine);
	find allocatedApplicationInstances(appType, appInstance, _);
}

pattern applicationBehavior(appType : ApplicationType, stateMachine : StateMachine) {
	ApplicationType.behavior(appType, stateMachine);
}

pattern deploymentApplicationBehavior(depApp : DeploymentApplication, depBehavior : DeploymentBehavior) {
	DeploymentApplication.behavior(depApp, depBehavior);
}

pattern mappedStateMachine(stateMachine : StateMachine, depBehavior : DeploymentBehavior, appInstance : ApplicationInstance, depApp : DeploymentApplication) {
	find allocatedStateMachines(appInstance, stateMachine);
	find mappedApplicationInstance(appInstance, depApp, _, _);
	find cps2depTrace(_, _, stateMachine, depBehavior);
	find deploymentApplicationBehavior(depApp, depBehavior);
}

pattern monitoredStateMachine(stateMachine : StateMachine) {
	find mappedStateMachine(stateMachine, _depBehavior, _appInstance, _depApp);
}

pattern unmappedStateMachine(appInstance : ApplicationInstance, stateMachine : StateMachine, depApp : DeploymentApplication) {
	find allocatedStateMachines(appInstance, stateMachine);
	find mappedApplicationInstance(appInstance, depApp, _, _);
	neg find mappedStateMachine(stateMachine, _, appInstance, _);
}

pattern deletedStateMachine(depBehavior : DeploymentElement, trace : CPS2DeploymentTrace) {
	find applicationBehavior(appType, stateMachine);
	neg find allocatedApplicationInstances(appType, _, _);
	find cps2depTrace(_, trace, stateMachine, depBehavior);
} or {
	neg find applicationBehavior(_, stateMachine);
	DeploymentBehavior(depBehavior);
	find cps2depTrace(_, trace, stateMachine, depBehavior);
}

/* ============================================
 * ==== 		                 		   ====
 * ====			   STATE RULES 		 	   ====
 * ====		                 			   ====
 * ============================================ */

pattern allocatedStateMachineState(stateMachine : StateMachine, state : State) {
	find allocatedStateMachines(_, stateMachine);
	find stateMachineState(stateMachine, state);
}

pattern stateMachineState(stateMachine : StateMachine, state : State) {
	StateMachine.states(stateMachine, state);
}

pattern behaviorState(depBehavior : DeploymentBehavior, depState : BehaviorState) {
	DeploymentBehavior.states(depBehavior, depState);
}

pattern mappedState(state : State, depState : BehaviorState, stateMachine : StateMachine, depBehavior : DeploymentBehavior) {
	find allocatedStateMachineState(stateMachine, state);
	find mappedStateMachine(stateMachine, depBehavior, _, _);
	find cps2depTrace(_, _, state, depState);
	find behaviorState(depBehavior, depState);
}

pattern monitoredState(state : State, stateMachine : StateMachine) {
	find mappedState(state, _depState, stateMachine, _depBehavior);
}

pattern unmappedState(state : State, stateMachine : StateMachine, depBehavior : DeploymentBehavior) {
	find allocatedStateMachineState(stateMachine, state);
	find mappedStateMachine(stateMachine, depBehavior, _, _);
	neg find mappedState(state, _, stateMachine, depBehavior);
}

pattern deletedState(depState : DeploymentElement, trace : CPS2DeploymentTrace) {
	find stateMachineState(stateMachine, state);
	neg find allocatedStateMachines(_, stateMachine);
	find cps2depTrace(_, trace, state, depState);
} or {
	neg find stateMachineState(_, state);
	BehaviorState(depState);
	find cps2depTrace(_, trace, state, depState);
}

/* ============================================
 * ==== 		                 		   ====
 * ====			 TRANSITION RULES	 	   ====
 * ====		                 			   ====
 * ============================================ */

pattern allocatedStateMachineTransition(stateMachine : StateMachine, source : State, transition : Transition, target : State) {
	find allocatedStateMachineState(stateMachine, source);
	find allocatedStateMachineState(stateMachine, target);
	find stateTransition(source, transition, target);
}

pattern stateTransition(source : State, transition : Transition, target : State) {
	State.outgoingTransitions(source, transition);
	Transition.targetState(transition, target);
}

pattern behaviorTransition(depBehavior : DeploymentBehavior, depTransition : BehaviorTransition) {
	DeploymentBehavior.transitions(depBehavior, depTransition);
}

pattern behaviorStateOutgoing(depState : BehaviorState, depTransition : BehaviorTransition) {
	BehaviorState.outgoing(depState, depTransition);
}

pattern mappedTransitionSourceTarget(transition : Transition, depSource : BehaviorState, depTarget : BehaviorState, depBehavior : DeploymentBehavior) {
	find mappedState(source, depSource, stateMachine, depBehavior);
	find mappedState(target, depTarget, stateMachine, depBehavior);
	find stateTransition(source, transition, target);
}

pattern mappedTransition(transition : Transition, depTransition : BehaviorTransition, depBehavior : DeploymentBehavior) {
	find allocatedStateMachineTransition(stateMachine, source, transition, target);
	find mappedState(source, _, stateMachine, depBehavior);
	find mappedState(target, _, stateMachine, depBehavior);
	find cps2depTrace(_, _, transition, depTransition);
	find behaviorTransition(depBehavior, depTransition);
}

pattern monitoredTransition(transition : Transition) {
	find mappedTransition(transition, _depTransition, _depBehavior);
}

pattern unmappedTransition(transition : Transition, depBehavior : DeploymentBehavior) {
	find allocatedStateMachineTransition(stateMachine, source, transition, target);
	find mappedState(source, _, stateMachine, depBehavior);
	find mappedState(target, _, stateMachine, depBehavior);
	neg find mappedTransition(transition, _, depBehavior);
}

pattern deletedTransition(depTransition : DeploymentElement, trace : CPS2DeploymentTrace) {
	find stateTransition(source, transition, _);
	neg find allocatedStateMachineState(_, source);
	find cps2depTrace(_, trace, transition, depTransition);
} or {
	find stateTransition(_, transition, target);
	neg find allocatedStateMachineState(_, target);
	find cps2depTrace(_, trace, transition, depTransition);
} or {
	neg find stateTransition(_, transition, _);
	BehaviorTransition(depTransition);
	find cps2depTrace(_, trace, transition, depTransition);
}

/* ============================================
 * ==== 		                 		   ====
 * ====			   ACTION RULES	 		   ====
 * ====		                 			   ====
 * ============================================ */

pattern sendTransitionAppSignal(transition : Transition, app : java String, signal : java String) {
 	Transition.action(transition, action);
 	app == eval(SignalUtil.getAppId(action));
 	signal == eval(SignalUtil.getSignalId(action));
}

pattern waitTransitionAppSignal(transition : Transition, appType : ApplicationType, signal : java String) {
 	find applicationBehavior(appType, stateMachine);
	find stateMachineState(stateMachine, source);
	find stateTransition(source, transition, _);
 	Transition.action(transition, action);
 	check(
 		SignalUtil.isWait(action)
 	);
 	signal == eval(SignalUtil.getSignalId(action));
}

pattern triggerPair(trigger : Transition, target : Transition) {
	find sendTransitionAppSignal(trigger, appId, signal);
	find waitTransitionAppSignal(target, waitApp, signal);
	ApplicationType.identifier(waitApp, appId);
}

pattern deploymentTrigger(depSendTransition : DeploymentElement, depWaitTransition : DeploymentElement) {
	find triggerPair(sendTransition, waitTransition);
	find cps2depTrace(_, _, sendTransition, depSendTransition);
	find cps2depTrace(_, _, waitTransition, depWaitTransition);
}	

pattern trigger(depSendTransition : BehaviorTransition, depWaitTransition : BehaviorTransition) {
	find deploymentTrigger(depSendTransition, depWaitTransition);
	find deploymentApplicationTransition(depSendApp, depSendTransition);
	find deploymentApplicationTransition(depWaitApp, depWaitTransition);
	find communicatingDeploymentAppInstances(depSendApp, depWaitApp);
}

pattern behaviorTransitionTrigger(depSendTransition : BehaviorTransition, depWaitTransition : BehaviorTransition) {
	BehaviorTransition.trigger(depSendTransition, depWaitTransition);
}

pattern unmappedTrigger(depSendTransition : BehaviorTransition, depWaitTransition : BehaviorTransition){
	neg find behaviorTransitionTrigger(depSendTransition, depWaitTransition);
	find trigger(depSendTransition, depWaitTransition);
}

pattern deletedTrigger(depSendTransition : BehaviorTransition, depWaitTransition : BehaviorTransition){
	find behaviorTransitionTrigger(depSendTransition, depWaitTransition);
	neg find trigger(depSendTransition, depWaitTransition);
}

pattern deploymentApplicationTransition(depApp : DeploymentApplication, depTransition : BehaviorTransition) {
	find deploymentApplicationBehavior(depApp, depBehavior);
	find behaviorTransition(depBehavior, depTransition);
}

pattern communicatingDeploymentAppInstances(sourceApp : DeploymentElement, targetApp : DeploymentElement) {
	find cps2depTrace(_, _, sourceAppInstance, sourceApp);
	find cps2depTrace(_, _, targetAppInstance, targetApp);
	find communicatingAppInstances(sourceAppInstance, targetAppInstance);
}

pattern communicatingAppInstances(sourceAppInstance : ApplicationInstance, targetAppInstance : ApplicationInstance) {
	find applicationInstance(_, sourceAppInstance, sourceHostInstance);
	find applicationInstance(_, targetAppInstance, targetHostInstance);
	find reachableHosts(sourceHostInstance, targetHostInstance);
}

pattern hostCommunication(fromHost : HostInstance, toHost : HostInstance) {
	HostInstance.communicateWith(fromHost, toHost);
}

pattern reachableHosts(source : HostInstance, target : HostInstance) {
	find hostCommunication(source, target);
} or {
	HostInstance(source);
	HostInstance(target);
	source == target;
}